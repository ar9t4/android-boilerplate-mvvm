plugins {
    id 'com.android.application'

    id 'kotlin-android'

    // used for @Parcelize annotation
    id 'kotlin-parcelize'

    // used for data binding
    id 'kotlin-kapt'

    // used for navigation safe args
    id 'androidx.navigation.safeargs.kotlin'

    // used for dependency injection
    id 'dagger.hilt.android.plugin'
}

android {
    compileSdkVersion = rootProject.compileSdkVersion
    buildToolsVersion = rootProject.buildToolsVersion

    defaultConfig {
        applicationId "com.android.boilerplate"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode 1
        versionName "1.0.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    bundle {
        language {
            // locale wise splits
            enableSplit = false
        }
        density {
            // density wise splits
            enableSplit = true
        }
        abi {
            // architecture wise splits
            enableSplit = true
        }
    }

    Properties properties = new Properties()
    if (rootProject.file('gradle.properties').canRead()) {
        properties.load(rootProject.file("gradle.properties").newDataInputStream())
    }

    signingConfigs {
        release {
            storeFile rootProject.file(properties.getProperty("RELEASE_STORE_FILE"))
            storePassword properties.getProperty("RELEASE_STORE_PASSWORD")
            keyAlias properties.getProperty("RELEASE_KEY_ALIAS")
            keyPassword properties.getProperty("RELEASE_KEY_PASSWORD")
            // optional
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    buildTypes {
        debug {
            buildConfigField("String", "BASE_URL", properties.getProperty("BASE_URL_DEBUG"))
        }
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            buildConfigField("String", "BASE_URL", properties.getProperty("BASE_URL_RELEASE"))
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        dataBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    configurations {
        configureEach {
           //exclude group: 'androidx.lifecycle', module: 'lifecycle-viewmodel', class: 'androidx.lifecycle.ViewModelLazy'
        }
    }
}

dependencies {
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    // appcompat
    implementation "androidx.appcompat:appcompat:$rootProject.appcompatVersion"

    // material
    implementation "com.google.android.material:material:$rootProject.materialVersion"

    // constraint layout
    implementation "androidx.constraintlayout:constraintlayout:$rootProject.constraintLayoutVersion"

    // navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$rootProject.navigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$rootProject.navigationVersion"

    // room: database
    implementation "androidx.room:room-runtime:$rootProject.roomVersion"
    implementation "androidx.room:room-ktx:$rootProject.roomVersion"
    // noinspection KaptUsageInsteadOfKsp
    kapt "androidx.room:room-compiler:$rootProject.roomVersion"

    // hilt
    implementation "com.google.dagger:hilt-android:$rootProject.hiltVersion"
    // noinspection KaptUsageInsteadOfKsp
    kapt "com.google.dagger:hilt-android-compiler:$rootProject.hiltVersion"

    // sms retriever api: sms otp verification apis
    implementation "com.google.android.gms:play-services-base:$rootProject.smsVerificationVersion"
    implementation "com.google.android.gms:play-services-auth-api-phone:$rootProject.servicesAuthApiPhoneVersion"

    // work manager
    implementation "androidx.work:work-runtime-ktx:$rootProject.workVersion"

    // intuit: dimens for dynamic layout building
    implementation "com.intuit.sdp:sdp-android:$rootProject.intuitVersion"
    implementation "com.intuit.ssp:ssp-android:$rootProject.intuitVersion"

    // gson: serialization/deserialization of Java Objects into JSON and back
    implementation "com.google.code.gson:gson:$rootProject.gsonVersion"

    // retrofit: network api calling
    implementation "com.squareup.retrofit2:retrofit:$rootProject.retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$rootProject.retrofitVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$rootProject.okhttpVersion"

    // glide: image loading
    implementation "com.github.bumptech.glide:glide:$rootProject.glideVersion"
    // noinspection KaptUsageInsteadOfKsp
    kapt "com.github.bumptech.glide:compiler:$rootProject.glideVersion"

    // custom tabs: chrome tabs
    implementation "com.android.support:customtabs:$rootProject.chromeTabsVersion"

    // google play services: location
    implementation "com.google.android.gms:play-services-location:$rootProject.playServicesLocationVersion"
}